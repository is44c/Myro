' Scribbler robot demonstration code
' Copyright 2005, Element Products, Inc.
'
'  {$STAMP BS2}
'  {$PBASIC 2.5}

DATA          "Scribby"

' I/O Declarations
LightRight    PIN 0
LightCenter   PIN 1
LightLeft     PIN 2
LineEnable    PIN 3
LineRight     PIN 4
LineLeft      PIN 5
ObsRx         PIN 6
Stall         PIN 7

LedRight      PIN 8
LedCenter     PIN 9
LedLeft       PIN 10
Speaker       PIN 11
MotorRight    PIN 12
MotorLeft     PIN 13
ObsTxRight    PIN 14
ObsTxLeft     PIN 15

' I/O Initialization

HIGH LightRight
HIGH LightCenter
HIGH LightLeft
LOW LineEnable
LOW LedRight
LOW LedCenter
LOW LedLeft
LOW Speaker
LOW MotorRight
LOW MotorLeft
LOW ObsTxRight
LOW ObsTxLeft

'BAUD              CON 84  '9600
BAUD               CON 6  '38400

OUTPIN             CON 16
INPIN              CON 16

GET_INPUT          CON 1
GET_OPEN_LEFT      CON 2
GET_OPEN_RIGHT     CON 3
GET_STALL          CON 4
GET_LIGHT_LEFT     CON 5
GET_LIGHT_CENTER   CON 6
GET_LIGHT_RIGHT    CON 7
GET_LINE_RIGHT     CON 8
GET_LINE_LEFT      CON 9
GET_NAME           CON 10

SET_MOTORS_OFF     CON 20
SET_MOTORS         CON 21
SET_LED_LEFT_ON    CON 22
SET_LED_LEFT_OFF   CON 23
SET_LED_CENTER_ON  CON 24
SET_LED_CENTER_OFF CON 25
SET_LED_RIGHT_ON   CON 26
SET_LED_RIGHT_OFF  CON 27
SET_SPEAKER        CON 28
SET_SPEAKER_2      CON 29
SET_NAME           CON 30
SET_LED_ALL_ON     CON 31
SET_LED_ALL_OFF    CON 32
GET_LIGHT_ALL      CON 33
GET_IR_ALL         CON 34
GET_LINE_ALL       CON 35
GET_ALL            CON 36
SET_LOUD           CON 37
SET_QUIET          CON 38

TIMEOUT            CON 1000 ' in ms
PACKET_LENGTH      CON 9
NAME_LENGTH        CON 8

' Global Variables

motor_r VAR Word
motor_l VAR Word
var3    VAR Word

name    VAR Byte(NAME_LENGTH)
inCmd   VAR Byte(PACKET_LENGTH)
temp    VAR Byte
temp2   VAR Byte
quiet   VAR Byte

inCmd(PACKET_LENGTH-1) = 0
quiet = 0

' turn off LEDs
LedCenter = 0
LedRight  = 0
LedLeft   = 0

INIT:
  ' Used to flash the middle LED with short timeout:
  IF (LedCenter = 1) THEN LedCenter = 0 ELSE LedCenter = 1

BEGIN:
  SERIN INPIN, BAUD, TIMEOUT, INIT, [STR inCmd\PACKET_LENGTH]
  LOOKDOWN inCmd(0), = [GET_INPUT, GET_LINE_RIGHT, GET_LINE_LEFT, GET_LIGHT_LEFT, GET_LIGHT_CENTER, GET_LIGHT_RIGHT,  GET_OPEN_LEFT, GET_OPEN_RIGHT, GET_STALL, GET_NAME, SET_MOTORS_OFF, SET_MOTORS, SET_SPEAKER, SET_SPEAKER_2, SET_LED_LEFT_ON, SET_LED_LEFT_OFF, SET_LED_CENTER_ON, SET_LED_CENTER_OFF, SET_LED_RIGHT_ON, SET_LED_RIGHT_OFF, SET_NAME, SET_LED_ALL_ON, SET_LED_ALL_OFF, GET_LIGHT_ALL, GET_IR_ALL, GET_LINE_ALL, GET_ALL, SET_LOUD, SET_QUIET], temp
  BRANCH temp,         [GetInput,  GetLineRight,   GetLineLeft,   GetLightLeft,   GetLightCenter,   GetLightRight,    GetObsLeft,    GetObsRight,    GetStall,  GetName,  MotorsOff,      Motors,     SetSpeaker,  SetSpeaker2,   OnLeftLED,       OffLeftLED,       OnCenterLED,       OffCenterLED,       OnRightLED,       OffRightLED,       SetName,  OnAllLED,       OffAllLED,       GetLightAll,   GetIRAll,   GetLineAll,   GetAll,  SetLoud,  SetQuiet]
TOP:
  SEROUT OUTPIN, BAUD, [STR inCmd\1]              ' echo command to tell we are done
  IF (LedCenter = 1) THEN LedCenter = 0 ELSE LedCenter = 1
  GOTO BEGIN

SetQuiet:
   quiet = 1
   GOTO TOP

SetLoud:
   quiet = 0
   GOTO TOP

GetInput:
     SEROUT OUTPIN, BAUD, [INL]
     GOTO TOP

GetLineRight:
     HIGH LineEnable
     PAUSE 2
     SEROUT OUTPIN, BAUD, [LineRight]
     LOW LineEnable
     GOTO TOP

GetLineLeft:
     HIGH LineEnable
     PAUSE 2
     SEROUT OUTPIN, BAUD, [LineLeft]
     GOTO TOP

GetLineAll:
     HIGH LineEnable
     PAUSE 2
     SEROUT OUTPIN, BAUD, [LineLeft, LineRight]
     GOTO TOP

GetLightLeft:
     HIGH LightLeft
     PAUSE 3
     RCTIME LightLeft, 1, var3
     SEROUT OUTPIN, BAUD, [var3.HIGHBYTE, var3.LOWBYTE]
     GOTO TOP

GetLightCenter:
     HIGH LightCenter
     PAUSE 3
     RCTIME LightCenter, 1, var3
     SEROUT OUTPIN, BAUD, [var3.HIGHBYTE, var3.LOWBYTE]
     GOTO TOP

GetLightRight:
     HIGH LightRight
     PAUSE 3
     RCTIME LightRight, 1, var3
     SEROUT OUTPIN, BAUD, [var3.HIGHBYTE, var3.LOWBYTE]
     GOTO TOP

GetLightAll:
     HIGH LightLeft
     HIGH LightCenter
     HIGH LightRight
     PAUSE 3
     RCTIME LightLeft, 1, var3
     RCTIME LightCenter, 1, motor_l
     RCTIME LightRight, 1, motor_r
     SEROUT OUTPIN, BAUD, [var3.HIGHBYTE, var3.LOWBYTE, motor_l.HIGHBYTE, motor_l.LOWBYTE, motor_r.HIGHBYTE, motor_r.LOWBYTE]
     GOTO TOP

GetObsRight:
     FREQOUT ObsTxRight, 1, 40500
     temp = ObsRX
     LOW ObsTxRight
     SEROUT OUTPIN, BAUD, [temp]
     GOTO TOP

GetObsLeft:
     FREQOUT ObsTxLeft, 1, 40500
     temp = ObsRX
     LOW ObsTxLeft
     SEROUT OUTPIN, BAUD, [temp]
     GOTO TOP

GetIRAll:
     FREQOUT ObsTxLeft, 1, 40500
     temp = ObsRX
     LOW ObsTxLeft

     FREQOUT ObsTxRight, 1, 40500
     temp2 = ObsRX
     LOW ObsTxRight

     SEROUT OUTPIN, BAUD, [temp, temp2]
     GOTO TOP

GetAll:
     'IR
     FREQOUT ObsTxLeft, 1, 40500
     temp = ObsRX
     LOW ObsTxLeft

     FREQOUT ObsTxRight, 1, 40500
     temp2 = ObsRX
     LOW ObsTxRight

     'Light
     HIGH LightLeft
     HIGH LightCenter
     HIGH LightRight
     PAUSE 3
     RCTIME LightLeft, 1, var3
     RCTIME LightCenter, 1, motor_l
     RCTIME LightRight, 1, motor_r

     'Line
     HIGH LineEnable
     PAUSE 2

     SEROUT OUTPIN, BAUD, [temp, temp2, var3.HIGHBYTE, var3.LOWBYTE,
                           motor_l.HIGHBYTE, motor_l.LOWBYTE, motor_r.HIGHBYTE, motor_r.LOWBYTE,
                           LineLeft, LineRight, Stall]
     GOTO TOP

GetStall:
     IF (Stall = 0) THEN  SEROUT OUTPIN, BAUD, [0] ELSE SEROUT OUTPIN, BAUD, [1]
     GOTO TOP

GetName:
     FOR temp = 0 TO NAME_LENGTH - 1
        READ temp, name(temp)
     NEXT
     SEROUT OUTPIN, BAUD, [STR name\NAME_LENGTH]

     GOTO TOP

SetName:
     FOR temp = 0 TO NAME_LENGTH - 1
        WRITE temp, inCmd(1 + temp)
     NEXT
     GOTO TOP

MotorsOff:
     motor_r = 2000
     motor_l = 2000
     PULSOUT MotorRight, motor_r
     PULSOUT MotorLeft, motor_l
     GOTO TOP

SetSpeaker:
     motor_r = inCmd(1)
     motor_r = (motor_r << 8) | inCmd(2)
     motor_l = inCmd(3)
     motor_l = (motor_l << 8) | inCmd(4)
     IF (quiet = 1) THEN
      FREQOUT Speaker, motor_r, $00
     ELSE
      FREQOUT Speaker, motor_r, motor_l
     ENDIF
     GOTO TOP

SetSpeaker2:
     motor_r = inCmd(1)
     motor_r = (motor_r << 8) | inCmd(2)

     motor_l = inCmd(3)
     motor_l = (motor_l << 8) | inCmd(4)

     var3 = inCmd(5)
     var3 = (var3 << 8) | inCmd(6)

     IF (quiet = 1) THEN
      FREQOUT Speaker, motor_r, $00
     ELSE
      FREQOUT Speaker, motor_r, motor_l, var3
     ENDIF
     GOTO TOP

Motors:
     motor_r = (inCmd(1) + 100) * 10
     motor_l = (inCmd(2) + 100) * 10
     PULSOUT MotorRight, motor_r
     PULSOUT MotorLeft, motor_l
     GOTO TOP

OnLeftLED:
     LedLeft = 1
     GOTO TOP

OffLeftLED:
     LedLeft = 0
     GOTO TOP

OnCenterLED:
     LedCenter = 1
     GOTO TOP

OffCenterLED:
     LedCenter = 0
     GOTO TOP

OnRightLED:
     LedRight = 1
     GOTO TOP

OffRightLED:
     LedRight = 0
     GOTO TOP

OnAllLED:
     LedLeft = 1
     LedRight = 1
     LedCenter = 1
     GOTO TOP

OffAllLED:
     LedLeft = 0
     LedRight = 0
     LedCenter = 0
     GOTO TOP
